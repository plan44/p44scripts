// Rantomega2 default main script: WORK IN PROGRESS

removeledchains()
addledchain("WS2813.GRB:/dev/ledchain1:2:0:2:0:1") // two LEDs
lrg.configure(ledchaincover()).set("fullframe",true)

glob leftled
glob rightled

lrg.clear()

leftled = makeview({
  type: 'plain',
  x: 1,
  y: 0,
  dx: 1,
  dy: 1,
  color: '#000',
  fullframe: true
})
lrg.addview(leftled)

rightled = makeview({
  type: 'plain',
  x: 0,
  y: 0,
  dx: 1,
  dy: 1,
  color: '#000',
  fullframe: true
})
lrg.addview(rightled)

//rightled.animator('alpha').from(180).repeat(0,true).runto(255,3)
//leftled.animator('alpha').from(255).repeat(0,true).runto(180,3)

// setup motors
try {
  system('insmod /flash/p44-stepper.ko stepper0=0,1,3,17,2,16 stepper1=0,1,5,11,4,15')
}
catch as e {
  log(4, "insmod err = %s", e)
}

var normalspeed =5000
var slowspeed = 10000

var min_to

function moveminutes(mins, speed)
{
  var steps = mins * 2359; // ~ 294.912 * 8
  writefile('/sys/class/stepper/stepper0/stepinterval', speed)
  writefile('/sys/class/stepper/stepper0/currentstep', 0)
  writefile('/sys/class/stepper/stepper0/targetstep', steps)
  abort(min_to)
  concurrent as min_to {
    delay(30+10*mins)
    writefile('/sys/class/stepper/stepper0/power', 0)
  }
}


var hr_to

function movehours(hours, speed)
{
  var steps = hours * 983; // ~ 122.88 * 8
  writefile('/sys/class/stepper/stepper1/stepinterval', speed)
  writefile('/sys/class/stepper/stepper1/currentstep', 0)
  writefile('/sys/class/stepper/stepper1/targetstep', steps)
  abort(hr_to)
  concurrent as hr_to {
    delay(30+10*hours)
    writefile('/sys/class/stepper/stepper1/power', 0)
  }
}


var manualmode = false

on (every(0:01, 0:01)) {
  if (!manualmode) moveminutes(1, normalspeed)
}

on (every(1:00, 1:00)) {
  if (!manualmode) movehours(1, normalspeed)
}


var manual_to

function manualop()
{
  manualmode = true
  abort(manual_to)
  concurrent as manual_to {
    delay(15)
    manualmode = false
  }
}


var leftbutton = device('leftbutton').button(0)
var leftheld = false

on (leftbutton) evaluating as clicks {
  log("left button click = %d", clicks)
  if (clicks==1) {
    movehours(1, normalspeed);
    manualop()
  }
  else if (clicks==5) {
    movehours(1, slowspeed);
    leftheld = true
    manualmode = true
  }
  else if (clicks==0 && leftheld) {
    movehours(0, slowspeed);
    leftheld = false
    manualmode = false
  }
}

var rightbutton = device('rightbutton').button(0)
var rightheld = false

on (rightbutton) evaluating as clicks {
  log("right button click = %d", clicks)
  if (clicks==1) {
    moveminutes(1, normalspeed);
    manualop()
  }
  else if (clicks==5) {
    moveminutes(1, slowspeed);
    rightheld = true
    manualmode = true
  }
  else if (clicks==0 && rightheld) {
    moveminutes(0, slowspeed);
    rightheld = false
    manualmode = false
  }
}
