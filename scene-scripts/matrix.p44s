// "Matrix" (not yet, really needs improvement, but sort of...)

// Note: this scene script needs FW x.7.0.37 or later

// How to use:
// - create a LED light of type "canvas" (set "Feature Config" to "canvas")
//   for this effect to appear
// - put this script into a scene of your choice
// - make sure the light has a non-zero brightness in this scene
// - call the scene, the scene script will execute and generate the effect

view.clear()

concurrent
{
  var numstreaks = 0
  while(true) {
    concurrent {
      numstreaks++
      // greenish
      threadvar sat = random(0.6,1)
      threadvar hue = random(100,140,1)
      // - trails has random brightness
      threadvar bgcol = hsv(hue,sat,random(0.3,0.8))
      // - drop is full brightness
      threadvar dropcol = hsv(hue,sat,1)
      // random start point and length
      threadvar x = random(0, view.dx-1)
      threadvar l = random(7,15,1)
      threadvar y = random(l, view.dy-1)
      // random speed
      threadvar s = random(0.02,0.06)
      // draw the drop and its trail
      for (threadvar i=0; i<l; i++) {
        if (i<l-1) {
          // head: drop
          view.color = dropcol
          view.dot(x,y-i-1)
        }
        // trailing tail
        view.color = bgcol
        view.dot(x,y-i)
        delay(s)
      }
      numstreaks--
    }
    // how fast to start a new drop
    delay(random(0.1,0.3))
    // safety to prevent too many drops/threads in parallel
    if (numstreaks>50) delay(10)
  }
}
